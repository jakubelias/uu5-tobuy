description 'A template project used for implementing uuApp server.'

apply plugin: 'java'
apply plugin: "com.sourcemuse.mongo"
apply plugin: 'war'
apply plugin: 'uu.appg01'

// repositories setup
repositories {
  maven { url uuProdRepository }
  maven { url uuBetaRepository }
  //TODO Only for development remove during release
  maven { url "http://192.168.4.209:8081/nexus/content/repositories/uaf-dev/" }
}

// java properties setup
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
  all*.exclude module: 'spring-boot-starter-logging'
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-log4j2"
  compile "uu.appg01:uu_appg01_server:${uuAppg01ServerVersion}"
  compile "net.minidev:json-smart:2.2.1"
  compile "org.modelmapper:modelmapper:1.1.0"

  testCompile "uu.appg01:uu_appg01_core-test_support:${uuAppg01ServerVersion}"
}

bootRepackage {
  enabled = false
}

// testing with embedded MongoDb
mongo {
  System.setProperty("os.arch", "x86_64")
  //workarround for https://github.com/flapdoodle-oss/de.flapdoodle.embed.mongo/issues/167
  mongoVersion '3.4.3'
}

test {
  runWithMongoDb = true
}

// artifacts which will be published to Nexus repository
uuPublishArtifacts {
  sources = true
  javadoc = true
  reports = false
  tests = false
  testSources = false
}

uuPublish {
  repositories.mavenDeployer {
    repository(url: uuDevReleaseRepository)
    snapshotRepository(url: uuDevSprintRepository)
  }
}

// deployment to C3
war {
  archiveName = project.name + '.war'
  from('public') {
    into('WEB-INF/classes/public')
  }
}

uuDeploySettings {
  // Deployment configuration location. Variable env is automatically resolved by plugin depending on project's property 'env', default value is 'prod'.
  uuCloudDeployConfig = rootProject.file("config/uucloud-deploy-config-${env}.json")
}

// mapping and persistence json generation
task generateMappingJson(type: JavaExec) {
  main = 'uu.app.server.internal.generator.CommandMappingGenerator'
  classpath = sourceSets.main.runtimeClasspath
  args = ["-bp", "${uuSubAppPackageName},uu.app.workspace,uu.app.binarystore", "-vuc", "config/mappings-vuc.json", "-out", "${projectDir}/config/mappings.json"]
}

task copyMappingJsonToResources(type: Copy) {
  from "${projectDir}/config/mappings.json"
  into "${buildDir}/resources/main/config/"
}


task generatePersistenceJson(type: JavaExec) {
  main = 'uu.app.datastore.schema.PersistenceSchemaRunner'
  classpath = sourceSets.main.runtimeClasspath
  args = ["-bp", "${uuSubAppPackageName},uu.app.datastore,uu.app.objectstore,uu.app.binarystore,uu.app.workspace", "-out", "config/persistence.json"]
}

processResources.doLast {
  tasks.generateMappingJson.execute()
  tasks.copyMappingJsonToResources.execute()
  tasks.generatePersistenceJson.execute()
}
