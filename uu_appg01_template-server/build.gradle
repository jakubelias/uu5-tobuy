description 'A template project used for implementing uuApp server.'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "com.sourcemuse.mongo"
apply plugin: 'war'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'checkstyle'
apply plugin: "jacoco"
apply plugin: 'build-dashboard'
apply plugin: 'project-report'
apply plugin: 'uu.appg01.deploy.plugin' // TODO name convention

// repositories setup
repositories {
  maven { url uuNexusRepository }
}

nexus {
  sign = false
  repositoryUrl = uuNexusReleaseRepository          //(RELEASED stable versions)
  snapshotRepositoryUrl = uuNexusSnapshotRepository // (SNAPSHOT build versions)
}

extraArchive {
  sources = true
  tests = true
  javadoc = true
}

// java properties setup
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
  imports {
    mavenBom 'io.spring.platform:platform-bom:Brussels-SR2'
    mavenBom 'org.apache.logging.log4j:log4j-bom:2.8.2'
  }
}

dependencies {
  compile project(':uu_appg01_template-client')

  compile("org.springframework.boot:spring-boot-starter-web")
  compile("org.springframework.boot:spring-boot-devtools")
  compile("org.springframework.boot:spring-boot-configuration-processor")
  compile("com.fasterxml.jackson.core:jackson-databind")

  testCompile('org.springframework.boot:spring-boot-starter-test')
}

bootRepackage {
  enabled = false
}

mongo {
  System.setProperty("os.arch", "x86_64")
  //workarround for https://github.com/flapdoodle-oss/de.flapdoodle.embed.mongo/issues/167
  mongoVersion '3.4.3'
  logging 'console'
}

//test {
//  runWithMongoDb = true
//}

// reports setup
buildDashboard {
  dependsOn(jacocoTestReport)
}

checkstyle {
  configFile = rootProject.file('config/checkstyle.xml')
  ignoreFailures = true
}

tasks.withType(Checkstyle) {
  reports {
    xml.enabled false
  }
}

task uuReport {
  group 'uu'
  buildDashboard.mustRunAfter check, jacocoTestReport
  dependsOn 'test', 'jacocoTestReport', 'check', 'buildDashboard'
}

// deployment
war {
  archiveName = project.name + '.war'
}

uuDeploySetting {
  appBoxLocationUri = project.appBoxLocationUri
}


